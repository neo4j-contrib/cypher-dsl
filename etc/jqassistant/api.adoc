[[arch-rules.api]]
[role=group,includesConstraints="arch-rules.api:*"]

==== API

===== General considerations

We use https://github.com/apiguardian-team/apiguardian[@API Guardian] to keep track of what we expose as public or internal API.
To keep things both clear and concise, we restrict the usage of those annotations to interfaces, classes (only public methods and constructors:
and annotations.

[[arch-rules.api:api-guardian-usage]]
[source,cypher,role="constraint"]
.@API Guardian annotations must not be used on fields
----
MATCH (c:Java)-[:ANNOTATED_BY]->(a)-[:OF_TYPE]->(t:Type {fqn: 'org.apiguardian.api.API'}),
      (p)-[:DECLARES]->(c)
WHERE c:Member AND NOT (c:Constructor OR c:Method)
RETURN p.fqn, c.name
----

Public interfaces, classes or annotations are either part of internal or public API and therefore must have a status:

[[arch-rules.api:api-guardian-api-concept]]
[source,cypher,role="concept",verify=rowCount,rowCountMin=0]
.Define which Java artifacts are part of internal or public API
----
MATCH (c:Java)-[:ANNOTATED_BY]->(a)-[:OF_TYPE]->(t:Type {fqn: 'org.apiguardian.api.API'}),
      (a)-[:HAS]->({name: 'status'})-[:IS]->(s)
WHERE ANY (label IN labels(c) WHERE label in ['Interface', 'Class', 'Annotation'])
WITH  c, trim(split(s.signature, ' ')[1]) AS status
WITH  c, status,
      CASE status
        WHEN 'INTERNAL' THEN 'Internal'
        ELSE 'Public'
      END AS type
MERGE (a:Api {type: type, status: status})
MERGE (c)-[:IS_PART_OF]->(a)
RETURN c,a
----

===== Internal API

While we are pretty clear about the intended use of our classes (being experimental, public API or strictly internal),
we want to make sure that no-one can coincidentally inherit from internal classes that we couldn't restrict to default
package visibility:

[[arch-rules.api:internal]]
[source,cypher,role="constraint",requiresConcepts="arch-rules.api:api-guardian-api-concept"]
.Non abstract, public classes that are only part of internal API must be final
----
MATCH (c:Class)-[:IS_PART_OF]->(:Api {type: 'Internal'})
WHERE c.visibility = 'public'
  AND coalesce(c.abstract, false) = false
  AND NOT exists(c.final)
RETURN c.name
----
